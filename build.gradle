buildscript {
    // Manifest version information!
    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0
    def versionBuild = 0 // bump for dogfood builds, public betas, etc.

    ext.versions = [
            'compileSdk': 26,
            'buildTools': '26.0.2',
            'minSdk': 21,
            'minSdkInstant' : 21,
            'targetSdk': 26,
            'kotlin': '1.2.20',

            'versionCode' : versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild,
            'versionName' : "${versionMajor}.${versionMinor}.${versionPatch}",

            'supportLibrary': '26.1.0',
            'constraintLayout': '1.0.2',
            'googlePlayServices': '11.8.0',
            'dagger': '2.13',
            'leakCanary': '1.5.1',
            'mosby': '3.1.0',
            'retrofit': '2.3.0',
            'permissionsDispatcher': '3.0.1'
    ]

    ext.deps = [
            'kotlin': [
                    'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jre7:$versions.kotlin",
                    'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
            ],
            'support': [
                    'v4': "com.android.support:support-v4:${versions.supportLibrary}",
                    'v13': "com.android.support:support-v13:${versions.supportLibrary}",
                    'appCompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",
                    'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
                    'design': "com.android.support:design:${versions.supportLibrary}",
                    'recyclerView': "com.android.support:recyclerview-v7:${versions.supportLibrary}",
                    'cardView': "com.android.support:cardview-v7:${versions.supportLibrary}",
                    'palette': "com.android.support:palette-v7:${versions.supportLibrary}",
                    'constraintLayout': "com.android.support.constraint:constraint-layout:${versions.constraintLayout}",
            ],
            'dagger': [
                    'runtime': "com.google.dagger:dagger:${versions.dagger}",
                    'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
            ],
            'daggerAndroid': [
                    'runtime': "com.google.dagger:dagger-android-support:${versions.dagger}",
                    'compiler':"com.google.dagger:dagger-android-processor:${versions.dagger}"
            ],
            'leakCanary': [
                    'real': "com.squareup.leakcanary:leakcanary-android:${versions.leakCanary}",
                    'noOp': "com.squareup.leakcanary:leakcanary-android-no-op:${versions.leakCanary}",
            ],
            'mosby': [
                    'core': "com.hannesdorfmann.mosby3:mvp:${versions.mosby}",
                    'viewState': "com.hannesdorfmann.mosby3:viewstate:${versions.mosby}",
            ],
            'retrofit': [
                    'base': "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                    'gson': "com.squareup.retrofit2:converter-gson:${versions.retrofit}",
            ],
            'permissionsDispatcher': [
                    'runtime': "com.github.hotchemi:permissionsdispatcher:${versions.permissionsDispatcher}",
                    'compiler': "com.github.hotchemi:permissionsdispatcher-processor:${versions.permissionsDispatcher}",
            ],
            'googlePlayServices': [
                    'location': "com.google.android.gms:play-services-location:${versions.googlePlayServices}",
            ],
            'fabric':[
                    "gradlePlugin": "io.fabric.tools:gradle:1.25.1",
                    'crashlytics': 'com.crashlytics.sdk.android:crashlytics:2.8.0@aar'
            ],
            'test': [
                    'junit': "junit:junit:4.12",
                    'mockito': "org.mockito:mockito-core:2.10.0",
                    'mockitoKotlin': "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
            ],
            'okHttpLogginInterceptor': 'com.squareup.okhttp3:logging-interceptor:3.9.0',
            'instantApps': "com.google.android.instantapps:instantapps:1.1.0",
            'timber': 'com.jakewharton.timber:timber:4.6.0',
            'mpAndroidChart': "com.github.PhilJay:MPAndroidChart:v3.0.2",
            'stetho': 'com.facebook.stetho:stetho-okhttp3:1.5.0',
    ]

    ext.keys = [
            'debug': [
                    'storePassword' : 'android',
                    'keyAlias' : 'androiddebugkey',
                    'keyPassword' : 'android',
                    'storeFile' : rootProject.file('key/debug.keystore')
            ],
            'release': [
                    'propertiesFile': rootProject.file('key/release.properties'),
                    'storeFile' : rootProject.file('key/release.keystore')
            ]
    ]

    if (keys.release.propertiesFile.exists()) {
        Properties keyProps = new Properties()
        keyProps.load(new FileInputStream(keys.release.propertiesFile))

        keys.release.storePassword = keyProps["storePass"]
        keys.release.keyAlias = keyProps["alias"]
        keys.release.keyPassword = keyProps["pass"]

        println 'Release key information loaded from "key/release.properties".'
    } else {
        keys.release.storePassword = keys.debug.storePassword
        keys.release.keyAlias = keys.debug.keyAlias
        keys.release.keyPassword = keys.debug.keyPassword
        keys.release.storeFile = keys.debug.storeFile

        println 'Failed to load release key information from "key/release.properties". Using the DEBUG key.'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath deps.kotlin.gradlePlugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" } // for MpAndroidChart
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
