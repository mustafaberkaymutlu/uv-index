buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath deps.fabric.gradlePlugin
    }
}

apply plugin: 'com.android.feature'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion versions.compileSdk
    baseFeature true

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        versionCode versions.versionCode
        versionName versions.versionName
    }

    signingConfigs {
        debugKey {
            storePassword keys.debug.storePassword
            keyAlias keys.debug.keyAlias
            keyPassword keys.debug.keyPassword
            storeFile keys.debug.storeFile
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debugKey
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../proguard-rules.pro'
        }
    }
}

crashlytics { instantAppSupport true }

dependencies {
    feature project(':query')
    feature project(":autocomplete")
    application project(':app')

    api deps.kotlin.stdlib

    api deps.instantApps

    // Support
    api deps.support.appCompat
    api deps.support.constraintLayout
    api deps.support.design
    api deps.support.cardView

    // TODO Move Google Play Services out of base
    // investigate "resource integer/google_play_services_version not found" error
    api deps.googlePlayServices.location

    // Dagger 2
    api deps.dagger.runtime
    kapt deps.dagger.compiler

    // Mosby
    api deps.mosby.core
    api deps.mosby.viewState

    // Retrofit
    api deps.retrofit.base
    api deps.retrofit.gson
    api deps.okHttpLogginInterceptor

    // Others
    api deps.timber
    implementation(deps.fabric.crashlytics) { transitive = true }
}
